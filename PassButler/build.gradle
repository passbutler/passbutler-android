apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        // Compile with pinned Java 7 (avoid changing `misc.xml` when cold-start Android Studio)
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "de.sicherheitskritisch.passbutler"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Added custom build field to get compile time
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/room_schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            useProguard false
            debuggable true
            minifyEnabled false

            manifestPlaceholders = [usesCleartextTraffic:true]
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [usesCleartextTraffic:false]
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Kotlin Coroutines for Android
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    // Default Google views like `Snackbar`
    implementation "com.google.android.material:material:1.1.0-alpha06"

    // Android Constraint Layout
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    // Android Lifecycle and ViewModel components
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"

    // Android Room components (including Kotlin Coroutine support)
    implementation "androidx.room:room-runtime:2.1.0-beta01"
    implementation "androidx.room:room-ktx:2.1.0-beta01"
    kapt "androidx.room:room-compiler:2.1.0-beta01"

    // RetroFit 2 + Kotlin Coroutine Adapter
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.0-alpha-2"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.2"

    // Mockk.io
    testImplementation "io.mockk:mockk:1.9.3"

    // JSON implementations for unit testing
    testImplementation "org.json:json:20180813"
}
