apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "com.squareup.sqldelight"
apply plugin: "de.mannodermaus.android-junit5"

ext.gitCommitCount = {
    return "git rev-list HEAD --count".execute().text.trim().toInteger()
}

ext.gitCommitHashShort = {
    return "git rev-parse --short HEAD".execute().text.trim()
}

android {
    sourceSets.all {
        java.srcDirs += ["src/$name/kotlin"]
    }

    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        // Required minimum Java 8 for OkHttp3 used in Retrofit
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Required minimum Java 8 API for "fragment-ktx" library 1.2.0+
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    // Avoid "More than one file was found with OS independent path" problem
    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "de.passbutler.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount()
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Added custom build fields
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        buildConfigField "String", "BUILD_REVISION_HASH", "\"" + gitCommitHashShort() + "\""
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false

            manifestPlaceholders = [usesCleartextTraffic: true]
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            manifestPlaceholders = [usesCleartextTraffic: false]
        }
    }
}

dependencies {
    implementation project(":PassButlerCommon")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Kotlin Coroutines for Android
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7"

    // TinyLog logger
    def tinylogVersion = "2.1.2"
    implementation "org.tinylog:tinylog-api-kotlin:$tinylogVersion"
    implementation "org.tinylog:tinylog-impl:$tinylogVersion"

    // SQLDelight
    implementation "com.squareup.sqldelight:android-driver:1.4.0"

    // Google Material theme/components
    implementation "com.google.android.material:material:1.1.0"

    // Android widgets
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    // Android Lifecycle and ViewModel components
    def lifecycleVersion = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    implementation "androidx.fragment:fragment-ktx:1.2.5"

    // Since no Java 8 is used this makes no sense
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:2.2.0"

    // Android Biometrics framework
    implementation "androidx.biometric:biometric:1.0.1"

    // Android Preference framework
    implementation "androidx.preference:preference:1.1.1"

    // JUnit 5
    def junitVersion = "5.6.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    // Mockk.io
    testImplementation "io.mockk:mockk:1.10.0"
}
