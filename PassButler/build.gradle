apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'

ext.gitCommitCount = {
    return "git rev-list HEAD --count".execute().text.trim().toInteger()
}

ext.gitCommitHashShort = {
    return "git rev-parse --short HEAD".execute().text.trim()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        // Required minimum Java 8 for OkHttp3 used in Retrofit
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "de.sicherheitskritisch.passbutler"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount()
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Added custom build fields
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        buildConfigField "String", "BUILD_REVISION_HASH", "\"" + gitCommitHashShort() + "\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/room_schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false

            manifestPlaceholders = [usesCleartextTraffic: true]
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [usesCleartextTraffic: false]
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Kotlin Coroutines for Android
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3"

    // Google Material theme/components
    implementation "com.google.android.material:material:1.1.0"

    // Android widgets
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    // Android Lifecycle and ViewModel components
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // Since no Java 8 is used this makes no sense
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:2.2.0"

    // Android Room components (including Kotlin Coroutine support)
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    // Android Biometrics framework
    implementation "androidx.biometric:biometric:1.0.1"

    // Android Preference framework
    implementation "androidx.preference:preference:1.1.0"

    // Retrofit with OkHttp
    implementation "com.squareup.retrofit2:retrofit:2.7.1"
    implementation "com.squareup.okhttp3:okhttp:4.4.0"

    implementation "org.tinylog:tinylog-api-kotlin:2.0.1"
    implementation "org.tinylog:tinylog-impl:2.0.1"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"

    // Mockk.io
    testImplementation "io.mockk:mockk:1.9.3"

    // JSON implementations for unit testing
    testImplementation "org.json:json:20190722"
}